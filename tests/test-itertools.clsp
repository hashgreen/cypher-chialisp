(a (mod

  (
    iterable
    iterable_of_two_vars
    iterables
  )

  (include cypher/constants.clsp)
  (include cypher/hashlib.clsp)
  (include cypher/itertools.clsp)
  (include cypher/math.clsp)
  (include cypher/utils.clsp)

  (include tests/utils.clsp)

  (defun add-one-to-var var (+ var 1))
  (defun add-two-vars (var1 var2) (+ var1 var2))

  (defun-inline inline-add-two-vars (var1 var2) (+ var1 var2))

  (defun-inline test-length ()
    (@assert
      (@assert-equal
        (cf.length (list)) 0)
      (@assert-equal
        (cf.length (list 1 2 3 4)) 4)
        "test-itertools.test-length"))

  (defun-inline test-range ()
    (@assert
      (@assert-equal
        (cf.range 0 0) (list))
      (@assert-equal
        (cf.range 0 1) (list 0))
      (@assert-equal
        (cf.range 0 3) (list 0 1 2))
      (@assert-equal
        (cf.range 0 10) (list 0 1 2 3 4 5 6 7 8 9))
      (@assert-equal
        (cf.range 3 10) (list 3 4 5 6 7 8 9))
      (@assert-equal
        (cf.range 10 3) (list))
      "test-itertools.test-range"))

  (defun-inline test-slice ()
    (@assert
      (@assert-equal
        (cf.slice (list) 0 0) (list))
      (@assert-equal
        (cf.slice (list) 0 10) (list))
      (@assert-equal
        (cf.slice (list 1 2 3 4) 0 0) (list))
      (@assert-equal
        (cf.slice (list 1 2 3 4) 0 3) (list 1 2 3))
      (@assert-equal
        (cf.slice (list 1 2 3 4) 0 10) (list 1 2 3 4))
      (@assert-equal
        (cf.slice (list 1 2 3 4) 3 10) (list 4))
      (@assert-equal
        (cf.slice (list 1 2 3 4) 10 0) (list))
      "test-itertools.test-slice"))

  (defun-inline test-reverse ()
    (@assert
      (@assert-equal
        (cf.reverse (list))
        (list))
      (@assert-equal
        (cf.reverse (list 1 2 3 4))
        (list 4 3 2 1))
      "test-itertools.test-reverse"))

  (defun-inline test-chain ()
    (@assert
      (@assert-equal
        (@cf.chain (list 100 200 300) (list 400 500 600))
        (list 100 200 300 400 500 600))
      (@assert-equal
        (@cf.chain (list 100 200 300) (list 400 500 600) (list 700 800 900))
        (list 100 200 300 400 500 600 700 800 900))
      (@assert-equal
        (cf.chain iterables)
        (list 100 200 300 400 500 600))
      "test-itertools.test-chain"))

  (defun-inline test-map ()
    (@assert
      (@assert-equal
        (@cf.map (lambda var (+ var 1)) 100 200 300)
        (list 101 201 301))
      (@assert-equal
        (@cf.map add-one-to-var 100 200 300)
        (list 101 201 301))
      (@assert-equal
        (cf.map (lambda var (+ var 1)) iterable)
        (list 101 201 301))
      (@assert-equal
        (cf.map add-one-to-var iterable)
        (list 101 201 301))
      (@assert-equal
        (cf.map add-two-vars iterable_of_two_vars)
        (list 300 700 1100))
      ; this doesn't work yet as `@assert-fail` works in a sandbox,
      ; and we have not found a way to propagate `defun-inline` and fixtures.
      ;
      ; (@assert-fail
      ;   (cf.map inline-add-two-vars iterable_of_two_vars))
      "test-itertools.test-map"))

  (defun is-even var (= (r (divmod var 2)) 0))

  (defun-inline test-filter ()
    (@assert
      (@assert-equal
        (cf.filter (lambda var (= (r (divmod var 2)) 0)) (list 1 2 3 4 5 6 7 8 9 10))
        (list 2 4 6 8 10))
      (@assert-equal
        (cf.filter is-even (list 1 2 3 4 5 6 7 8 9 10))
        (list 2 4 6 8 10))
      "test-itertools.test-filter"))

  (defun add-arg-if-not-equal-to-var (arg agg var)
    (if (= arg var) agg (+ arg agg)))

  (defun-inline test-reduce ()
    (@assert
      (@assert-equal
        (@cf.reduce (lambda (arg agg) (+ arg agg)) 0 () 1 2 3 4) 10)
      (@assert-equal
        (@cf.reduce (lambda (arg agg) (* arg agg)) 1 () 1 3 5 7) 105)
      (@assert-equal
        (@cf.reduce add-two-vars 32 () 1 2 3 4) 42)
      (@assert-equal
        (@cf.reduce add-arg-if-not-equal-to-var 0 (list 4) 1 2 3 4) 6)
      (@assert-equal
        (cf.reduce (lambda (arg agg) (+ arg agg)) 0 () (list 1 2 3 4)) 10)
      (@assert-equal
        (cf.reduce (lambda (arg agg) (* arg agg)) 1 () (list 1 3 5 7)) 105)
      (@assert-equal
        (cf.reduce add-two-vars 32 () (list 1 2 3 4)) 42)
      (@assert-equal
        (cf.reduce add-arg-if-not-equal-to-var 0 (list 4) (list 1 2 3 4)) 6)
      "test-itertools.test-reduce"))

  (defun-inline test-itertools ()
    (@assert
      (test-length)
      (test-range)
      (test-slice)
      (test-reverse)
      (test-chain)
      (test-map)
      (test-filter)
      (test-reduce)
      "test-itertools"))

  (test-itertools)

) (list
    ; iterable
    (list 100 200 300)
    ; iterable_of_two_vars
    (list
      (list 100 200)
      (list 300 400)
      (list 500 600)
    )
    ; iterables
    (list
      (list 100 200 300)
      (list 400 500 600)
    )
  )
)
