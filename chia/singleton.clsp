(mod
  (
    SINGLETON_MOD
    INNER_PUZZLE

    lineage_proof
    amount
    inner_solution
  )

  (include cypher.clsp)
  (include chia/singleton-lib.clsp)

  (defun stager-three
    (singleton_mod lineage_proof id puzzle_hash inner_puzzle_hash amount inner_puzzle inner_solution)

    (@cf.concat
      (cf.AssertMyCoinID id)
      (SingletonMod.morph-conditions.do singleton_mod
        (a
          inner_puzzle
          (@cf.concat
            (SingletonTruth
              singleton_mod lineage_proof id puzzle_hash amount inner_puzzle_hash)
            inner_solution))
        0)))

  (defun stager-two
    (singleton_mod lineage_proof puzzle_hash inner_puzzle_hash amount inner_puzzle inner_solution)

    (stager-three
      singleton_mod
      lineage_proof
      (if (EveLineageProof.is-not lineage_proof)
        (SingletonMod.morph-coin-id singleton_mod
          (cf.Coin () puzzle_hash amount)
          lineage_proof
        )
        (if
          (=
            (SingletonMod.get-launcher-id singleton_mod)
            (cf.Coin.id@unsafe (cf.Coin
              (EveLineageProof.get-parent-id lineage_proof)
              (SingletonMod.get-launcher-puzzle-hash singleton_mod)
              (EveLineageProof.get-amount lineage_proof)
            ))
          )
          (cf.Coin.id@unsafe (cf.Coin
            (SingletonMod.get-launcher-id singleton_mod)
            puzzle_hash
            amount
          ))
          (x)
        )
      )
      puzzle_hash
      inner_puzzle_hash
      amount
      inner_puzzle
      inner_solution))

  (defun stager-one
    (singleton_mod lineage_proof inner_puzzle_hash amount inner_puzzle inner_solution)

    (stager-two
      singleton_mod
      lineage_proof
      (SingletonMod.morph-puzzle-hash singleton_mod inner_puzzle_hash)
      inner_puzzle_hash
      amount
      inner_puzzle
      inner_solution))

  (defun-inline main
    (singleton_mod inner_puzzle lineage_proof amount inner_solution)

    (if (logand amount 1)
      (stager-one
        singleton_mod lineage_proof (sha256tree#singleton-lib inner_puzzle) amount inner_puzzle inner_solution)
      (x)))

  (main SINGLETON_MOD INNER_PUZZLE lineage_proof amount inner_solution)
)
